plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'be.raft'
version = "${project_version}"

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
    maven {
        url = 'https://repo.mattmalec.com/repository/releases'
    }
}

dependencies {
    //Api
    implementation project(":api");

    //Networking
    implementation "io.netty:netty-all:4.1.94.Final"

    //Database
    implementation "org.mongodb:mongodb-driver-sync:4.10.1"

    //Logging
    implementation "ch.qos.logback:logback-classic:1.4.7"

    //Command
    implementation "com.mojang:brigadier:1.0.18"

    //Panel Integration
    implementation 'com.mattmalec:Pterodactyl4J:2.BETA_140'

    //Web
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
}

compileJava {
    options.encoding = "UTF-8"
}

shadowJar {
    manifest {
        attributes 'Main-Class': "${project_main_class}"
    }
    archiveBaseName.set('AtlasNetwork')
    archiveClassifier.set('')
}

//Only build shadowJar
build.dependsOn(shadowJar)
jar.enabled = false

tasks.register('run', JavaExec) {
    dependsOn build
    dependsOn (":api:buildTestModule")
    
    doFirst {
        File runDir = new File(project.rootDir, "run")
        if (!runDir.exists()) {
            runDir.mkdirs()
        }
        workingDir = runDir
    }
    mainClass.set("${project_main_class}")
    classpath = files(shadowJar.archiveFile)
    args "-devEnv", "-add-module", "../api/build/libs/TestModule-${project_version}.jar" // program arguments go here
}